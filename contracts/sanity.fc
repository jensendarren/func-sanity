#include "imports/stdlib.fc";
#include "message.fc";
const op::add = "op::add"c; 
const op::accumulate = "op::accumulate"c;  ;; tracker op code

;; storage variables
global slice ctx_owner; 
global int ctx_id; 
global int ctx_result; ;; the result of the add operation
global slice ctx_tracker_contract_addr;

() load_data() impure { 
    var ds = get_data().begin_parse(); 

    ctx_owner = ds~load_msg_addr();
    ctx_id = ds~load_uint(32); 
    ctx_result = ds~load_uint(32); 
    ctx_tracker_contract_addr = ds~load_msg_addr(); 

    ds.end_parse();
}

() save_data() impure {
    set_data( 
        begin_cell() 
            .store_slice(ctx_owner)
            .store_uint(ctx_id, 32) 
            .store_uint(ctx_result, 32)
            .store_slice(ctx_tracker_contract_addr)
            .end_cell() 
    );
}

;; custom sum function
(int) sum(int a, int b) { 
    return a + b; 
}

;; Before passing control to recv_internal TVM puts the following elements on stack.
;; my_balance -> Smart contract balance (in nanoTons)
;; msg_value -> Incoming message balance (in nanoTons)
;; in_msg_full -> Cell with an incoming message
;; in_msg_body -> Incoming message body, slice type
() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore all empty messages
        return ();
    }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4); ;; the first four bits of serialized message contain certain flags, one of which indicates whether the message is bounced
    ;; a message is bounced when a contract receives a bounceable message and throws during its processing
    ;; the bounced message is then returned to sender with `bounced` bit set, this is a way to handle errors in TON

    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }

    ;; Every message has a strict layout, thus by parsing it, we can get the senderâ€™s address
    ;; first, we need to read some tech flags and then take the address using load_msg_addr (from stdlib)
    slice sender_addr = cs~load_msg_addr();

    load_data(); ;; here we populate the storage variables

    ;; check the caller (sender_addr) is the contract owner (ctx_owner) first
    if (equal_slice_bits(sender_addr, ctx_owner)) {
        ;; in this case, it could also be done in the single op::increase handler, but generally you would want
        ;; to populate storage variables as soon as all preliminary checks which do not need storage pass
        int op = in_msg_body~load_uint(32); ;; by convention, the first 32 bits of incoming message is the op
        int query_id = in_msg_body~load_uint(64); ;; also by convention, the next 64 bits contain the "query id", although this is not always the case
        ;; some contracts do not use query_id at all and do not have it in their messages, but for this one we will be reading it, but not using it


        if (op == op::add) { ;; handle op::increase
            int a = in_msg_body~load_uint(32); ;; read 'a' value
            int b = in_msg_body~load_uint(32); ;; read 'b' value

            ctx_result = sum(a,b);

            save_data(); ;; when we change storage variables, we need to store the changed variables, so we call save_data()

            msg::send_result(
                ctx_tracker_contract_addr,
                ctx_result,
                op::accumulate,
                msg::send_mode::carry_remaining_value
            );

            return (); ;; this message is handled so we return
        }
    }

    throw(0xffff); ;; if the message contains an op that is not known to this contract, we throw
    ;; if the message is bounceable, the contract will then bounce the message to the sender
    ;; and the sender will receive unspent coins and will know that this message failed
    ;; provided of course that the sender has code to handle bounced messages
}

slice owner() method_id {
    load_data();
    return ctx_owner;
}

int get_result() method_id {
    load_data();
    return ctx_result; 
}

slice get_tracker_contract_addr() method_id {
    load_data();
    return ctx_tracker_contract_addr; 
}
